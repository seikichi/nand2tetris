// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    /** Initializes the keyboard. */
    function void init() {
        return;
    }

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(24576);
    }

    /**
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character
     * of the pressed key.
     */
    function char readChar() {
        var char c0, c1;
        do Output.printChar(0);
        while ((c1 = 0) | (c0 > 0)) {
            let c0 = Keyboard.keyPressed();
            if (c0 > 0) {
                let c1 = c0;
            }
        }
        do Output.printChar(String.backSpace());
        do Output.printChar(c1);
        return c1;
    }

    /**
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var String c, buf, nl, bs;
        let buf = String.new(80);
        let nl = String.newLine();
        let bs = String.backSpace();

        do Output.printString(message);
        while (true) {
            let c = Keyboard.readChar();

            if (c = nl) {
                return buf;
            } else {
                if (c = bs) {
                    do buf.eraseLastChar();
                } else {
                    let buf = buf.appendChar(c);
                }
            }
        }
    }

    /**
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces.
     */
    function int readInt(String message) {
        var String line;
        var int value;
        let line = Keyboard.readLine(message);
        let value = String.intValue(line);
        do line.dispose();
        return value;
    }
}
